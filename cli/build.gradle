plugins {
    id 'org.springframework.boot' version "4.0.1"
    id 'java'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.3'
}

java {
    sourceCompatibility = 21
    targetCompatibility = 21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':api')
    implementation 'info.picocli:picocli:4.7.7'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    archiveVersion = acdasmVersion
}

application {
    mainClass = 'org.applecommander.disassembler.cli.Main'
}

// Disable all Spring AOT processing
processAot.configure {
    enabled = false;
}
processTestAot.configure {
    enabled = false;
}

bootJar {
    archiveBaseName = 'acdasm'
    archiveVersion = acdasmVersion
    manifest {
        attributes 'Implementation-Title': "AppleCommander 'acdasm'",
                   'Implementation-Version': acdasmVersion
    }
}

graalvmNative {
    String osName = System.getProperty('os.name').toLowerCase().split()[0]
    String osArch = System.getProperty('os.arch').toLowerCase().split()[0]
    binaries {
        named("main") {
            imageName.set("acdasm-${osName}-${osArch}-${acdasmVersion}")
        }
    }
}
