plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

ext.isSnapshotVersion = version.endsWith("SNAPSHOT")
ext.isReleaseVersion = !ext.isSnapshotVersion

java {
    sourceCompatibility = 21
    targetCompatibility = 21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ini4j:ini4j:0.5.4'

    testImplementation platform('org.junit:junit-bom:6.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

jar {
    archiveVersion = acdasmVersion
    manifest {
        attributes 'Implementation-Title': "AppleCommander 'acdis'",
                   'Implementation-Version': acdasmVersion
    }
}

javadoc {
    title = "adasm ${acdasmVersion}"
    source = sourceSets.main.allJava
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "org.applecommander"
            artifactId = "acdasm"
            version = "${acdasmVersion}"

            from components.java

            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'AppleCommander Disassembler (acdasm)'
                description = 'A reusable Java-based API for disassembling Apple-related source code.'
                url = 'https://applecommander.github.io/'
                licenses {
                    license {
                        name = 'The GNU General Public License (GPL) Version 3, 29 June 2007'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'robgreene'
                        name = 'Rob Greene'
                        email = 'robgreene@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/AppleCommander/acdasm.git'
                    developerConnection = 'scm:git:git@github.com:AppleCommander/acdasm.git'
                    url = 'https://github.com/AppleCommander/acdasm'
                }
            }
            repositories {
                maven {
                    def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2"
                    def snapshotsRepoUrl = "https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"
                    url = isSnapshotVersion ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = findProperty('ossrhUsername')
                        password = findProperty('ossrhPassword')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
